/*** Lexical items ***/

/* Token definitions */

char_val : '"' {'\x20'-'\x21' | '\x23'-'\x7e'} ;

crlf : '\x10' '\x0a' ;

digit : '\x30'-'\x39' ;

num_val : '%' (_bin_val | _dec_val | _hex_val) ;

//oOm_digit : _OoMDigit ;

prose_val : '<' {'\x20'-'\x3d' | '\x3f'-'\x7e'} '>' ;

ruleName : _alpha {_alpha | _digit | '_'} ;

vchar : '\x21'-'\x7e' ;

wsp : ' ' | '\t' ;

/* Ignored tokens */

/* Regular definitions */

_alpha : 'A'-'Z' | 'a'-'z' ;

_bin_val : 'b' _OoMBit [_dotBit | '-' _OoMBit] ;

_bit : '0' | '1' ;

_dec_val : 'd' _OoMDigit [_dotDigit | '-' _OoMDigit] ;

_hex_val : 'x' _OoMHexDig [_dotHexDig | '-' _OoMHexDig] ;

_dotDigit : '.' _OoMDigit {'.' _OoMDigit} ;

_digit : '0'-'9' ;

_dotHexDig : '.' _OoMHexDig {'.' _OoMHexDig} ;

_hexdig : '0'-'9' | 'a'-'f' | 'A'-'F' ;

_OoMBit : _bit {_bit} ;

_dotBit : '.' _OoMBit {'.' _OoMBit} ;

_OoMDigit : _digit {_digit} ;

_OoMHexDig : _hexdig {_hexdig} ;

/*** Syntactic items ***/

RuleList
	: RuleList1 {RuleList1}
	;

RuleList1
	: Rule 
	| {C_wsp} C_nl
	;

Rule
	: ruleName Defined_as Elements C_nl
	;

Defined_as
	: {C_wsp} ("=" | "=/") {C_wsp}
	;

Elements
	: Alternation {C_wsp}
	;

C_wsp
	: wsp
	| C_nl wsp
	;

C_nl
	: Comment
	| crlf
	;

Comment
	: ";" {wsp | vchar} crlf
	;

Alternation
	: Concatenation {{C_wsp} [Concatenation]}
	;

Concatenation
	: Repetition {C_wsp {C_wsp} Repetition}
	;

Repetition
	: [Repeat] Element
	;

Repeat
	: digit {digit}
	| {digit} "*" {digit}
	;

Element
	: ruleName
	| Group
	| Option
	| char_val
	| num_val
	| prose_val
	;

Group
	: "(" {C_wsp} Alternation {C_wsp} ")"
	;

Option
	: "[" {C_wsp} Alternation {C_wsp} "]"
	;

